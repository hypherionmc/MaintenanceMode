plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'idea'
    id "me.hypherionmc.modutils.modpublisher" version "1.0.16"
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation project(":Common")

    implementation 'org.apache.commons:commons-compress:1.21'
    include 'org.apache.commons:commons-compress:1.21'
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching('*.mixins.json') {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}


task delDevJar {
    doLast {
        def tree = fileTree("build/libs")
        tree.include '**/*-dev.jar'
        tree.each { it.delete() }
    }
}

build.finalizedBy delDevJar


publisher {
    apiKeys {
        curseforge = System.getenv("CURSE_TOKEN")
        modrinth = System.getenv("MODRINTH_TOKEN")
    }

    curseID = curse_id
    modrinthID = modrinth_id
    versionType = "release"
    changelog = rootProject.file("changelog-fabric.md")
    version = "${minecraft_version}-${project.version}"
    displayName = "[FABRIC/QUILT 1.17.1] Maintenance Mode - ${project.version}"
    gameVersions = ["1.17.1"]
    loaders = ["fabric", "quilt"]
    artifact = remapJar

    curseDepends {
        required = ["fabric-api"]
    }

    modrinthDepends {
        required = ["P7dR8mSH"]
    }
}

publishMod.dependsOn(build)
